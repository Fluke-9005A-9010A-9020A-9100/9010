
PROGRAM 0   2419 BYTES : Merge Tape

   <BINARY>

PROGRAM 1   45 BYTES

   SYNC DATA
   READ PROBE
   RAMP @ 100FA
   RAMP @ FFFF
   EXECUTE PROGRAM 91
   DPY-LVL=$A SIG=$9 CNT=@8#

PROGRAM 2   179 BYTES

   REGD = 0
   SYNC DATA
   READ PROBE
   WALK @ FFFF = 1
   EXECUTE PROGRAM 91
   IF REGA = 5 GOTO 3
   SYNC ADDRESS
1: LABEL 1
   READ PROBE
   ATOG @ FFFF BIT REGD
   EXECUTE PROGRAM 91
   IF REGA = 5 GOTO 4
   INC REGD
   IF 10 > REGD GOTO 1
2: LABEL 2
   DPY-CANNOT IDENTIFY#
   GOTO F
3: LABEL 3
   READ PROBE
   DTOG @ FFFF = 00 BIT REGD
   EXECUTE PROGRAM 91
   IF REGA = 5 GOTO 5
   INC REGD
   IF 8 > REGD GOTO 3
   GOTO 2
4: LABEL 4
   DPY-ADDRESS BIT @D#
   GOTO F
5: LABEL 5
   DPY-DATA BIT @D#
F: LABEL F

PROGRAM 3   66 BYTES

   DPY-ENTER NUMBER #\1
   DPY-ENTER SEC NUMBER #\2
   REGB = 312
   EXECUTE PROGRAM 10
   DPY-@1 + @2= @3#

PROGRAM 4   216 BYTES

   REG5 = 4000000
   SYNC DATA
   REGF = 0
   DPY-ENTER LSB TO DECODE# \1
   REG2 = 1
   REG3 = REG1
   IF REG1 = 0 GOTO 2
1: LABEL 1
   SHL REG2
   DEC REG3
   IF REG3 > 0 GOTO 1
2: LABEL 2
   DPY-TESTING $F
   READ PROBE
   READ @ REGF
   READ PROBE
   IF REG0 AND REG5 = REG5 GOTO 3
   REGB = F2F
   EXECUTE PROGRAM 10
   IF 10000 > REGF GOTO 2
   DPY-CANNOT IDENTIFY#
   GOTO F
3: LABEL 3
   IF REG5 = 1000000 GOTO 4
   REG5 = 1000000
   REG4 = REGF
   GOTO 2
4: LABEL 4
   DEC REGF
   DPY-ADRESS= $4 TO $F#
F: LABEL F

PROGRAM 5   215 BYTES

1: LABEL 1
   DPY-PRESS UUT RESET#
   READ @ STS
   IF REGC AND 10 = 0 GOTO 1
2: LABEL 2
   READ @ STS
   IF REGC AND 10 > 0 GOTO 2
   WRITE @ 100FB = 90
   WRITE @ 100FA = EF
   REG1 = 0
   REG2 = FE
3: LABEL 3
   DPY-PRESS KEY @1
   READ @ 100F8
   IF REGE = FF GOTO 3
   IF REGE = REG2 GOTO 4
   DPY-BAD DATA= $E NOT= $2
   GOTO F
4: LABEL 4
   READ @ 100F8
   IF FF > REGE GOTO 4
   INC REG1
   REG2 = REG2 SHL INC AND FF
   IF 8 > REG1 GOTO 3
   DPY-PASS################
F: LABEL F

PROGRAM 6   244 BYTES

   WRITE @ 100FB = 90
   WRITE @ 100FA = 1
   DTOG @ 100F9 = 80 BIT 7
1: LABEL 1
   REG8 = 3409
   EXECUTE PROGRAM 92
   EXECUTE PROGRAM 94
   IF REG8 = 1 GOTO 3
   DPY-TEST IN PROGRESS#
   REG1 = 23
   READ PROBE
2: LABEL 2
   WRITE @ 100FA = 0
   DTOG @ 100F9 = 80 BIT 7
   WRITE @ 100FA = 1
   DTOG @ 100F9 = 80 BIT 7
   DEC REG1
   IF REG1 > 0 GOTO 2
   EXECUTE PROGRAM 91
   IF REG8 = 23 GOTO 4
   DPY-BAD COUNT= @8 NOT= 35#
   GOTO F
3: LABEL 3
   DPY-IS PROBE IN PLACE# ?2
   IF REG2 = 0 GOTO 1
   DPY-U52 OPEN#
   GOTO F
4: LABEL 4
   DPY-PASS#
F: LABEL F

PROGRAM 7   65 BYTES

   DPY-WAIT
   EXECUTE PROGRAM 94
   DPY-#
   IF REG8 = 1 GOTO 1
   DPY-PROBE SET
   GOTO F
   DPY-#
1: LABEL 1
   DPY-OPEN CIRCUIT
F: LABEL F
   DPY-#

PROGRAM 8   29 BYTES

   DPY-ENTER DECIMAL \1
   DPY-HEX= $1

PROGRAM 10   102 BYTES

   <BINARY>

PROGRAM 91   60 BYTES

   READ PROBE
   REG8 = REG0 AND 7F
   REG9 = REG0 SHR SHR SHR SHR SHR SHR SHR SHR AND FFFF
   REGA = REG0 SHR SHR SHR SHR SHR SHR SHR SHR SHR SHR SHR SHR SHR SHR SHR SHR 
SHR SHR SHR SHR SHR SHR SHR SHR AND 7

PROGRAM 92   44 BYTES

   REG1 = REG8 AND FF
   REG2 = REG8 SHR SHR SHR SHR SHR SHR SHR SHR AND FF
   DPY-PROBE U@2 PIN@1#

PROGRAM 93   140 BYTES

1: LABEL 1
   DPY-ENTER DEVICE NUMBER
   DPY-+<1-255># \1
   IF REG1 = 0 GOTO 1
   IF REG1 > FF GOTO 1
2: LABEL 2
   DPY-ENTER P1N NUMBER
   DPY-+<1-255># \8
   IF REG8 = 0 GOTO 2
   IF REG8 > FF GOTO 2
   REG8 = REG1 SHL SHL SHL SHL SHL SHL SHL SHL OR REG8
   DPY-REGISTER8= $8

PROGRAM 94   92 BYTES

   SYNC FREE-RUN
1: LABEL 1
   REG1 = 50
   REG2 = 20
2: LABEL 2
   READ PROBE
   IF REG0 AND 5000000 > 0 GOTO 3
   DEC REG1
   IF REG1 > 0 GOTO 2
   REG8 = 1
   GOTO F
3: LABEL 3
   READ PROBE
   IF REG0 = 2000000 GOTO 1
   DEC REG2
   IF REG2 > 0 GOTO 3
   REG8 = 0
F: LABEL F
