
PROGRAM 0   232 BYTES

   DPY-*** 6502 POD TESTS
   DPY-+ REV 1.0 ***#
   EXECUTE PROGRAM 97
   DPY-*** FLUKE CUSTOMER
   DPY-+ SERVICE ***#
   EXECUTE PROGRAM 97
   DPY-SET SW2 TO 6502
   DPY-+ THEN PRESS CONT#
   STOP
0: LABEL 0
   DPY-TEST? 1-POD NORMAL
   DPY-+ 2-POD RUN UUT
1: LABEL 1
   DPY-+#
   REG1 = 40
   DPY-+%1
2: LABEL 2
   IF REG1 = 40 GOTO 2
   IF REG1 = 1 GOTO 3
   IF REG1 = 2 GOTO 4
   GOTO 1
3: LABEL 3
   EXECUTE PROGRAM 64
   GOTO 0
4: LABEL 4
   EXECUTE PROGRAM 65
   GOTO 0

PROGRAM 1   9 BYTES

   READ PROBE
   READ PROBE
   REGC = REG0

PROGRAM 2   18 BYTES

   READ PROBE
   ATOG @ 0 BIT REGD REPT REPT REPT
   READ PROBE
   REGC = REG0

PROGRAM 3   24 BYTES

   READ PROBE
   DTOG @ FFFF = FF BIT REGD REPT REPT REPT
   READ PROBE
   REGC = REG0

PROGRAM 4   28 BYTES

   SYNC ADDRESS
   READ PROBE
   DTOG @ CTL = 00000000 BIT REGD REPT REPT REPT
   READ PROBE
   REGC = REG0

PROGRAM 5   13 BYTES

   READ PROBE
   EXECUTE PROGRAM 98
   READ PROBE
   REGC = REG0

PROGRAM 6   76 BYTES

   REG1 = 5
0: LABEL 0
   READ PROBE
   RAMP @ FFFF
   READ PROBE
   REGC = REG0
   REG0 = REG0 SHR SHR SHR SHR
   REG0 = REG0 SHR SHR SHR SHR AND FFFF
   IF REGA = REG0 GOTO 1
   GOTO 2
1: LABEL 1
   DEC REG1
   IF REG1 > 0 GOTO 0
2: LABEL 2

PROGRAM 7   20 BYTES

   SYNC ADDRESS
   READ PROBE
   WRITE @ FFFF = FF
   READ PROBE
   REGC = REG0

PROGRAM 64   1036 BYTES

0: LABEL 0
   DPY-ENTER STARTING TEST 1-13 ?
   DPY-+\1
   IF REG1 = 1 GOTO 1
   IF REG1 = 2 GOTO 2
   IF REG1 = 3 GOTO 3
   IF REG1 = 4 GOTO 4
   IF REG1 = 5 GOTO 5
   IF REG1 = 6 GOTO 6
   IF REG1 = 7 GOTO 7
   IF REG1 = 8 GOTO 8
   IF REG1 = 9 GOTO A
   IF REG1 = A GOTO C
   IF REG1 = B GOTO D
   IF REG1 = C GOTO E
   IF REG1 = D GOTO F
   GOTO 0
1: LABEL 1
   DPY-POWER SUPPLY CHECK#
   EXECUTE PROGRAM 97
   REG8 = 00041065
   EXECUTE PROGRAM 94
   REG8 = 00011063
   EXECUTE PROGRAM 94
2: LABEL 2
   DPY-CLOCK TEST#
   EXECUTE PROGRAM 97
   REG8 = 00051067
   EXECUTE PROGRAM 94
   REG8 = 00051068
   EXECUTE PROGRAM 94
   REG8 = 0005105F
   EXECUTE PROGRAM 94
3: LABEL 3
   DPY-STATUS CHECK#
   EXECUTE PROGRAM 97
   REG8 = 00011059
   EXECUTE PROGRAM 94
   REG8 = 0001105A
   EXECUTE PROGRAM 94
   REG8 = 0001105B
   EXECUTE PROGRAM 94
   REG8 = 0001105C
   EXECUTE PROGRAM 94
4: LABEL 4
   DPY-READ STATUS TEST-WAIT#
   EXECUTE PROGRAM 97
   REG8 = 00010819
   EXECUTE PROGRAM 92
   REG8 = 0000881A
   EXECUTE PROGRAM 92
   REG8 = 0000481B
   EXECUTE PROGRAM 92
   REG8 = 0000181C
   EXECUTE PROGRAM 92
   REG8 = 00010099
   EXECUTE PROGRAM 92
   REG8 = 0000809A
   EXECUTE PROGRAM 92
   REG8 = 0000409B
   EXECUTE PROGRAM 92
   REG8 = 0000109C
   EXECUTE PROGRAM 92
5: LABEL 5
   DPY-POWER SUPPLY STATUS TEST#
   EXECUTE PROGRAM 97
   REG8 = 00080000
   EXECUTE PROGRAM 92
   REG8 = 00080A00
   EXECUTE PROGRAM 92
6: LABEL 6
   DPY-CONTROL CHECK#
   EXECUTE PROGRAM 97
   REG8 = 00051060
   EXECUTE PROGRAM 94
   REG8 = 00011062
   EXECUTE PROGRAM 94
7: LABEL 7
   DPY-WRITE CONTROL TEST#
   EXECUTE PROGRAM 97
   REGD = 0
   REG8 = 00051120
   EXECUTE PROGRAM 94
   REG8 = 000411E2
   EXECUTE PROGRAM 94
8: LABEL 8
   DPY-ADDRESS TOGGLE TEST#
   EXECUTE PROGRAM 97
   REGD = 0
   REG8 = 00055081
9: LABEL 9
   EXECUTE PROGRAM 94
   INC REGD
   INC REG8
   IF 10 > REGD GOTO 9
A: LABEL A
   DPY-DATA TOGGLE TEST#
   EXECUTE PROGRAM 97
   REGD = 0
   REG8 = 00590D1
B: LABEL B
   EXECUTE PROGRAM 94
   INC REGD
   INC REG8
   IF 8 > REGD GOTO B
C: LABEL C
   DPY-BUS TEST#
   EXECUTE PROGRAM 97
   DPY-+-WAIT
   BUS TEST
D: LABEL D
   DPY-READ DATA TEST-WAIT#
   EXECUTE PROGRAM 97
   REG8 = 0800A2
   EXECUTE PROGRAM 90
   REG8 = 0801FF
   EXECUTE PROGRAM 90
E: LABEL E
   DPY-SIGNATURE STABILITY TEST#
   EXECUTE PROGRAM 97
   REG8 = 96EC8191
   EXECUTE PROGRAM 94
F: LABEL F
   DPY-FIXTURE ROM TEST#
   EXECUTE PROGRAM 97
   DPY-+ WAIT
   ROM TEST @ 800-FFF SIG 91CD
   DPY-*** NORMAL TEST
   DPY-+ COMPLETE ***#
   EXECUTE PROGRAM 97

PROGRAM 65   622 BYTES

   DPY-*** 6502 POD 'RUN UUT'
   DPY-+ TESTS ***#
   EXECUTE PROGRAM 97
0: LABEL 0
   DPY-'RUN UUT' CONTROL TESTS#
   EXECUTE PROGRAM 97
   RUN UUT @ 0
   DPY-TOUCH TP25 LOW
   DPY-+ THEN PRESS CONT#
   STOP
   REG8 = 00051060
   EXECUTE PROGRAM 94
   REG8 = 00011062
   EXECUTE PROGRAM 94
1: LABEL 1
   DPY-'RUN UUT' ADDRESS TESTS#
   EXECUTE PROGRAM 97
   REG8 = 00051041
2: LABEL 2
   EXECUTE PROGRAM 94
   INC REG8
   IF 00051045 > REG8 GOTO 2
   REG8 = 00041045
3: LABEL 3
   EXECUTE PROGRAM 94
   INC REG8
   IF 0004104B > REG8 GOTO 3
   REG8 = 0005104B
   EXECUTE PROGRAM 94
   REG8 = 0005104C
   EXECUTE PROGRAM 94
   REG8 = 0005104D
   EXECUTE PROGRAM 94
   REG8 = 0004104E
   EXECUTE PROGRAM 94
   REG8 = 0004104F
   EXECUTE PROGRAM 94
   REG8 = 00041050
   EXECUTE PROGRAM 94
4: LABEL 4
   DPY-'RUN UUT' DATA TESTS#
   EXECUTE PROGRAM 97
   REG8 = 00051051
5: LABEL 5
   EXECUTE PROGRAM 94
   INC REG8
   IF 00051059 > REG8 GOTO 5
6: LABEL 6
7: LABEL 7
   DPY-'RUN UUT' IRQ TEST#
   EXECUTE PROGRAM 97
   DPY-TOUCH TP26 LOW
   DPY-+ THEN PRESS CONT#
   STOP
   REG8 = 0005104F
   EXECUTE PROGRAM 94
8: LABEL 8
   DPY-'RUN UUT' NMI TEST#
   EXECUTE PROGRAM 97
   DPY-TOUCH TP27 LOW
   DPY-+ THEN PRESS CONT#
   STOP
   REG8 = 00051050
   EXECUTE PROGRAM 94
   DPY-*** RUN UUT TEST
   DPY-+ COMPLETE ***#
   EXECUTE PROGRAM 97

PROGRAM 90   283 BYTES

   REG2 = REG8 AND FF
   REG3 = REG8 SHR SHR SHR SHR
   REG3 = REG3 SHR SHR SHR SHR
   REG3 = REG3 AND FFFF
   READ @ REG3
   DPY-READ DATA $2=$E
   IF REG2 = REGE GOTO 6
   DPY-+ FAIL LOOP?#
   EXECUTE PROGRAM 98
   DPY-+#
   EXECUTE PROGRAM 98
0: LABEL 0
   DPY-+#
   REG1 = 40
   DPY-+%1
1: LABEL 1
   IF REG1 = 40 GOTO 1
   IF REG1 = 1C GOTO 2
   IF REG1 = 27 GOTO 2
   IF REG1 = 1D GOTO F
   IF REG1 = 25 GOTO F
   GOTO 0
2: LABEL 2
   REGB = 40
   DPY-+%B
3: LABEL 3
   READ @ REG3
   DPY-READ DATA $2=$E
   IF REG2 = REGE GOTO 4
   DPY-+ FAIL
   GOTO 5
4: LABEL 4
   DPY-+ PASS#
5: LABEL 5
   IF REGB = 40 GOTO 3
   IF REGB = 25 GOTO F
   DPY-+#
   GOTO 2
6: LABEL 6
   DPY-+ PASS#
   EXECUTE PROGRAM 98
F: LABEL F

PROGRAM 91   257 BYTES

   READ @ STS REPT
   REGC = REGC AND FF
   REGA = REG8 SHR SHR SHR SHR
   REGA = REGA SHR SHR SHR SHR
   REGA = REGA SHR SHR SHR SHR
   REGA = REGA AND FF
   IF REG9 > 0 GOTO 0
   DPY-POWER
   GOTO 1
0: LABEL 0
   DPY-TP@9
1: LABEL 1
   IF REG8 AND 800 = 800 GOTO 2
   CPL REGC
   REGC = REGC AND FF
   DPY-+ STATUS LOW=
   GOTO 3
2: LABEL 2
   DPY-+ STATUS HIGH=
3: LABEL 3
   IF REGA AND REGC = REGA GOTO 5
   IF REG8 AND 800 = 800 GOTO 4
   DPY-+HIGH FAIL
   GOTO F
4: LABEL 4
   DPY-+LOW FAIL
   GOTO F
5: LABEL 5
   IF REG8 AND 800 = 800 GOTO 6
   DPY-+LOW PASS#
   GOTO F
6: LABEL 6
   DPY-+HIGH PASS#
F: LABEL F

PROGRAM 92   485 BYTES

   REG9 = REG8 AND 3F
   IF REG8 AND 80 = 0 GOTO 3
0: LABEL 0
   DPY-JUMPER TP@9
   IF REG8 AND 100 = 100 GOTO 1
   DPY-+ LOW
   GOTO 2
1: LABEL 1
   DPY-+ HIGH
2: LABEL 2
   DPY-+ THEN PRESS CONT#
   STOP
3: LABEL 3
   IF REG8 AND 600 = 0 GOTO 4
   REGD = REG8 SHR SHR SHR SHR
   REGD = REGD SHR SHR SHR SHR
   REGD = REGD SHR AND 3
   DPY-HOLD SWITCH 
   DPY-+@D THEN PRESS CONT#
   STOP
4: LABEL 4
   EXECUTE PROGRAM 91
   IF REGA AND REGC = REGA GOTO B
   DPY-+ LOOP?#
   EXECUTE PROGRAM 98
   DPY-+#
   EXECUTE PROGRAM 98
5: LABEL 5
   DPY-+#
   REG1 = 40
   DPY-+%1
6: LABEL 6
   IF REG1 = 40 GOTO 6
   IF REG1 = 1C GOTO 7
   IF REG1 = 27 GOTO 7
   IF REG1 = 1D GOTO B
   IF REG1 = 25 GOTO B
   GOTO 5
7: LABEL 7
   REGB = 40
   DPY-+%B
8: LABEL 8
   EXECUTE PROGRAM 91
   IF REGA AND REGC = REGA GOTO 9
   GOTO A
9: LABEL 9
   DPY-+#
A: LABEL A
   IF REGB = 40 GOTO 8
   IF REGB = 25 GOTO B
   DPY-+#
   GOTO 7
B: LABEL B
   EXECUTE PROGRAM 98
C: LABEL C
   IF REG8 AND 80 = 80 GOTO D
   IF REG8 AND 600 > 0 GOTO E
   GOTO F
D: LABEL D
   DPY-REMOVE JUMPER
   DPY-+ THEN PRESS CONT#
   STOP
   GOTO F
E: LABEL E
   DPY-RELEASE SW@D
   DPY-+ THEN PRESS CONT#
   STOP
F: LABEL F

PROGRAM 93   441 BYTES

   IF REG8 AND 2000 = 2000 GOTO 1
   IF REG8 AND 1000 = 1000 GOTO 5
0: LABEL 0
   REGC = REGC SHR SHR SHR SHR
   REGC = REGC SHR SHR SHR SHR AND FFFF
   DPY-TP@9 SIG $A=$C
   GOTO F
1: LABEL 1
   REGC = REGC AND 7F
   REG2 = REGA AND 7F
   REG1 = REGA SHR SHR SHR SHR SHR
   REG1 = REG1 SHR SHR SHR AND 7F
   IF REG1 > REG2 GOTO 2
   IF REGC > REG2 GOTO 3
   IF REG1 > REGC GOTO 3
   GOTO 4
2: LABEL 2
   IF REG2 >= REGC GOTO 4
   IF REGC >= REG1 GOTO 4
3: LABEL 3
   DPY-TP@9 COUNT @1-@2 =@C
   GOTO F
4: LABEL 4
   DPY-TP@9 COUNT @1-@2 =@C
   REGC = REGA
   GOTO F
5: LABEL 5
   REGC = REGC SHR SHR SHR SHR
   REGC = REGC SHR SHR SHR SHR
   REGC = REGC SHR SHR SHR SHR
   REGC = REGC SHR SHR SHR SHR
   REGC = REGC SHR SHR SHR SHR
   REGC = REGC SHR SHR SHR SHR
   DPY-TP@9 LOGIC LVL 
   IF REGA AND 1 = 0 GOTO 6
   DPY-+H
6: LABEL 6
   IF REGA AND 2 = 0 GOTO 7
   DPY-+X
7: LABEL 7
   IF REGA AND 4 = 0 GOTO 8
   DPY-+L
8: LABEL 8
   DPY-+=
9: LABEL 9
   IF REGC AND 1 = 0 GOTO A
   DPY-+H
A: LABEL A
   IF REGC AND 2 = 0 GOTO B
   DPY-+X
B: LABEL B
   IF REGC AND 4 = 0 GOTO C
   DPY-+L
C: LABEL C
   IF REGC > 0 GOTO F
   DPY-+X
F: LABEL F

PROGRAM 94   338 BYTES

   REG9 = REG8 AND 3F
   REGA = REG8 SHR SHR SHR SHR SHR SHR SHR SHR
   REGA = REGA SHR SHR SHR SHR SHR SHR SHR SHR
   DPY-PROBE TP@9
   EXECUTE PROGRAM 96
   SYNC FREE-RUN
   IF REG8 AND C000 = 0 GOTO 0
   SYNC ADDRESS
   IF REG8 AND 4000 > 0 GOTO 0
   SYNC DATA
0: LABEL 0
   REG2 = REG8 SHR SHR SHR SHR SHR SHR AND 3F
   EXECUTE PROGRAM REG2
   EXECUTE PROGRAM 93
   IF REGA = REGC GOTO 7
   DPY-+ FAIL LOOP?#
   EXECUTE PROGRAM 98
   DPY-+#
   EXECUTE PROGRAM 98
1: LABEL 1
   DPY-+#
   REG1 = 40
   DPY-+%1
2: LABEL 2
   IF REG1 = 40 GOTO 2
   IF REG1 = 1C GOTO 3
   IF REG1 = 27 GOTO 3
   IF REG1 = 1D GOTO 8
   IF REG1 = 25 GOTO 8
   GOTO 1
3: LABEL 3
   REGB = 40
   DPY-+%B
4: LABEL 4
   REG2 = REG8 SHR SHR SHR SHR SHR SHR AND 3F
   EXECUTE PROGRAM REG2
   EXECUTE PROGRAM 93
   IF REGA = REGC GOTO 5
   DPY-+ FAIL
   GOTO 6
5: LABEL 5
   DPY-+ PASS#
6: LABEL 6
   IF REGB = 40 GOTO 4
   IF REGB = 25 GOTO 8
   DPY-+#
   GOTO 3
7: LABEL 7
   DPY-+ PASS#
   EXECUTE PROGRAM 98
8: LABEL 8
   EXECUTE PROGRAM 95

PROGRAM 95   64 BYTES

   SYNC FREE-RUN
0: LABEL 0
   REG1 = 4
1: LABEL 1
   READ PROBE
   IF REG0 AND 5000000 = 0 GOTO 2
   DPY-REMOVE PROBE
   GOTO 0
2: LABEL 2
   DEC REG1
   IF REG1 > 0 GOTO 1

PROGRAM 96   62 BYTES

   SYNC FREE-RUN
   REG1 = 6F
0: LABEL 0
   DEC REG1
   IF REG1 = 0 GOTO F
   REG2 = 4
1: LABEL 1
   READ PROBE
   IF REG0 AND 5000000 = 0 GOTO 0
   DEC REG2
   IF REG2 > 0 GOTO 1
F: LABEL F

PROGRAM 97   17 BYTES

0: LABEL 0
   INC REG1
   IF 4F > REG1 GOTO 0

PROGRAM 98   16 BYTES

0: LABEL 0
   INC REG1
   IF F > REG1 GOTO 0
